;;; -*- Mode: Lisp; Syntax: Common-Lisp -*-
;;; Code from Paradigms of AI Programming
;;; Copyright (c) 1991 Peter Norvig

;;; File auxfns.lisp: Auxiliary functions used by all other programs
;;; Load this file before running any other programs.

;;;; Implementation-Specific Details

(in-package :cl-tuples)

(eval-when (eval compile load)
;;  Make it ok to place a function definition on a built-in LISP symbol.
  #+allegro
  (dolist (pkg '(excl common-lisp common-lisp-user))
    (setf (excl:package-definition-lock (find-package pkg)) nil))
  #+clisp
  (dolist (pkg '(excl common-lisp common-lisp-user))
    (setf (ext:package-lock (find-package pkg)) nil))
  #+sbcl
  (dolist (pkg '(common-lisp common-lisp-user))
    (sb-ext:unlock-package (find-package pkg)))
  #+cmu
  (dolist (pkg '(extensions common-lisp common-lisp-user))
    (let ((pkg (find-package pkg)))
      (when pkg
	(setf (ext:package-lock pkg) nil)
	(setf (ext:package-definition-lock pkg) nil))))

;;   Don't warn if a function is defined in multiple files --
;;   this happens often since we refine several programs.
  #+Lispworks
  (setq *PACKAGES-FOR-WARN-ON-REDEFINITION* nil)

  #+LCL 
   (compiler-options :warnings nil)
  )

;;;; Macros (formerly in auxmacs.lisp: that file no longer needed)

(eval-when (load eval compile)

;;   (defmacro once-only (variables &rest body)
;;     "Returns the code built by BODY.  If any of VARIABLES
;;   might have side effects, they are evaluated once and stored
;;   in temporary variables that are then passed to BODY."
;;     (assert (every #'symbolp variables))
;;     (let ((temps nil))
;;       (dotimes (i (length variables)) (push (gensym) temps))
;;       `(if (every #'side-effect-free? (list .,variables))
;; 	(progn .,body)
;; 	(list 'let
;; 	 ,`(list ,@(mapcar #'(lambda (tmp var)
;; 			       `(list ',tmp ,var))
;; 			   temps variables))
;; 	 (let ,(mapcar #'(lambda (var tmp) `(,var ',tmp))
;; 		       variables temps)
;; 	   .,body)))))

  (defun side-effect-free? (exp)
    "Is exp a constant, variable, or function,
  or of the form (THE type x) where x is side-effect-free?"
    (or (atom exp) (constantp exp)
	(starts-with exp 'function)
	(and (starts-with exp 'the)
	     (side-effect-free? (third exp)))))

  (defmacro funcall-if (fn arg)
    (once-only (fn)
	       `(if ,fn (funcall ,fn ,arg) ,arg)))

  (defmacro read-time-case (first-case &rest other-cases)
    "Do the first case, where normally cases are
  specified with #+ or possibly #- marks."
    (declare (ignore other-cases))
    first-case)

  (defun rest2 (x)
    "The rest of a list after the first TWO elements."
    (rest (rest x)))

  (defun find-anywhere (item tree)
    "Does item occur anywhere in tree?"
    (if (atom tree)
	(if (eql item tree) tree)
	(or (find-anywhere item (first tree))
	    (find-anywhere item (rest tree)))))

  (defun starts-with (list x)
    "Is x a list whose first element is x?"
    (and (consp list) (eql (first list) x)))
  )

;;;; Auxiliary Functions

(setf (symbol-function 'find-all-if) #'remove-if-not)

(defun find-all (item sequence &rest keyword-args
                 &key (test #'eql) test-not &allow-other-keys)
  "Find all those elements of sequence that match item,
  according to the keywords.  Doesn't alter sequence."
  (if test-not
      (apply #'remove item sequence 
             :test-not (complement test-not) keyword-args)
      (apply #'remove item sequence
             :test (complement test) keyword-args)))

(defun partition-if (pred list)
  "Return 2 values: elements of list that satisfy pred,
  and elements that don't."
  (let ((yes-list nil)
        (no-list nil))
    (dolist (item list)
      (if (funcall pred item)
          (push item yes-list)
          (push item no-list)))
    (values (nreverse yes-list) (nreverse no-list))))

(defun maybe-add (op exps &optional if-nil)
  "For example, (maybe-add 'and exps t) returns
  t if exps is nil, exps if there is only one,
  and (and exp1 exp2...) if there are several exps."
  (cond ((null exps) if-nil)
        ((length=1 exps) (first exps))
        (t (cons op exps))))

;;; ==============================

(defun seq-ref (seq index)
  "Return code that indexes into a sequence, using
  the pop-lists/aref-vectors strategy."
  `(if (listp ,seq)
       (prog1 (first ,seq)
              (setq ,seq (the list (rest ,seq))))
       (aref ,seq ,index)))

(defun maybe-set-fill-pointer (array new-length)
  "If this is an array with a fill pointer, set it to
  new-length, if that is longer than the current length."
  (if (and (arrayp array)
           (array-has-fill-pointer-p array))
      (setf (fill-pointer array) 
            (max (fill-pointer array) new-length))))

;;; ==============================

;;; NOTE: In ANSI Common Lisp, the effects of adding a definition (or most
;;; anything else) to a symbol in the common-lisp package is undefined.
;;; Therefore, it would be best to rename the function SYMBOL to something 
;;; else.  This has not been done (for compatibility with the book).  

(defun symbol (&rest args)
  "Concatenate symbols or strings to form an interned symbol"
  (intern (format nil "狺狎珞┅ㄤ彐躅铄鳝簌礅镬é蝈篝狎珞⒚镱汜翦钺翦簌礅镬矧篝蜷铉麸骘蝽犷躅轭翦蝾邃簌礅镬磲脲簌礅镬ㄦ矧磲铋狺狎珞┅ㄤ彐躅灬篝扉篝⒁弭躜翳灬篝屐屙孱铒灬篝泔铙沐祆镦扉篝ㄦ轵篝灬篝扉篝┅换浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇ㄤ彐躅磲痧孱ㄦ扉篝⒘痧孱翳蝈篚祠镦汜祆轭骖镱遽汨屐屙孱镦扉篝涕脲磲疸镱怩躞弩狃疱钿轭篝遽镦钽镱惝ㄡ痧禊＇狃疱钿磲疸狎骖扉篝┅ㄤ彐躅黼扉篝⑸轶扉篝蝈趱蝾轸雉桢蝼轶蝈趱蝾翳扉篝镦ㄩ扉篝扉篝┅ㄤ彐躅骒狒翦ㄥ皓⑶弭蜷镦轫忮滗邃扉篝麸镱戾鲥镱禊┊磲痧孱＇黼扉篝屮皓ㄤ彐躅蜥钿镯屐箦瘵⑿殂蜥钿镯屐屙孱秕镦箦聃孱沐ㄥ祠箦蜥钿镯戾铉翳箦瘵┅换浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇ㄤ彐躅礤礅弪羼踽ㄩ翦扉篝礤礅弪轸屙扉篝呼弩＇羼踽飑换浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇ㄤ彐躅泔眇矬é蝈篝骢钽糸镱螬＇灬礅溽蝈漉沐＇骢钽犰骢钽糸镱烘蝻憝孱洪铋糸犰鲠祯┅换换澡腻怩珑轭硝麴豸漆汩扉豉ㄤ彐鲠溻绛殇螵铋⑸溴铘殒殄蝮躞邃怡溻纰ㄤ彐躅溻ㄩ骘蝽狒篝蜷铉蝈篝狎珞⑿蜷铘溴怩珑轭轭骘殒呐抡赡栳忮孱箴邈殒殄洚麒孱礤礅弪殇溻绛殇螵ㄦ蝈箬扉铄溴怩绛轱ㄡ痧禊＇骘蝽狒溴怩绛轱骘蝽狒篝蜷铉狎珞┅ㄤ彐躅溴怩é蝈篝殇螬⒂翎螋溻秕麴豸镱翳玳鲥殇螽箦翩溻绛殇螵躅轱殇溻绛殇螵┅ㄤ彐躅躅溴怩é蝈篝殇螬⒂麸溻镱翳殇螽组翳铒殇蟋篝镳溻犰麸珏翳弪箦翩溻绛殇螵ㄩ铛祆殇螬铋箦舡溟骀弪孱沐溻绛殇螵殇螬┅换浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇ㄤ彐躅溻绛轭溴铘ㄩ轭溴铘骘蝽狒篝蜷铉蝈篝狎珞⑿蜷铘轭溴铘邃溴怩珑轭轭骘殒呐抡赡栳忮孱箴邈殒殄洚麒孱礤礅弪殇溻绛殇螵ㄦ蝈箬扉铄溴怩绛轱ㄤ雉轫弩ㄩ轭溴铘痱轭溴怩绛轱┅ㄡ痧禊＇骘蝽狒溴怩绛轱骘蝽狒篝蜷铉狎珞┅换换辛栽乓土悦壬吻屏蒙躺再ㄤ彐泔铙翎铘驷殪铋飑ㄤ彐疳蜥礤翦铒忾钿轭珞Ж舂┅ㄤ彐躅疳舡磲翥疳趑弪轭瘐镳糸镱犰ㄢ轭溟铉铒忾钿轭珞┅⑼狒汨疳趑弪徵衢铙轭瘐轭翳泔铘屮镦翳忾钿轭珞ㄣ镱è羼忾钿轭珞驷殪驷殪è鲠蜷徕戾疳趑弪瞟磲翥璀鲠蜷徕戾疳趑弪轭瘐忾钿轭珞┅è羼疳趑弪轭瘐舂忾钿轭珞è犷ㄣ镱箴疳趑弪瞟ㄣ镱箴轭瘐舂疳舡磲翥蝈篝疳趑弪瞟蝈篝轭瘐舂疳舡磲翥ㄦ轵篝疳趑弪瞟ㄦ轵篝轭瘐舂忾钿轭珞┅驷殪┅ㄤ彐躅磲翥璀鲠蜷徕戾鲠轭瘐忾钿轭珞⒛镥至磲翥轭瘐艨阵弩矧躔溽翦螬犷蝈趱蝾忾钿轭珞戾è忾钿轭ㄧ弭忾钿轭鲠忾钿轭珞┅ㄣ镱è铒忾钿轭绌ㄥ翦钿忾钿轭珞鲠轭瘐忾钿轭珞┅è羼踽轭瘐ㄢ轭溟铉鲠忾钿轭绌忾钿轭珞驷殪┅┅ㄤ彐躅磲脲忾钿轭鲠鲠飑ㄣ镱鲠鲠飑ㄤ彐躅忾钿轭绛鲠ㄢ轭溟铉⑶弭翳鲠蜷徕戾疳螋镦箝铉戾忾钿轭绠ㄣ狎忾钿轭绌ㄤ彐躅忾钿轭绛鲠ㄢ轭溟铉⑶弭翳鲠祯疳螋镦箝铉戾忾钿轭绠ㄣ潋忾钿轭绌ㄤ彐躅珏舡忾钿轭鲠忾钿轭珞⑵轭鲠蜷徕戾鲠祯濠疳轵轭忾钿轭扉篝ㄡ篌镢鲠忾钿轭珞┅ㄤ彐躅祜镫躔鲠忾钿轭珞⑶弭翳鲠祯疳螋ㄦ矧鲠颟骝镯忾钿轭扉篝ㄢ轭溟铉鲠ㄧ弭忾钿轭鲠忾钿轭珞┅ㄤ彐躅屮翦钿忾钿轭珞鲠鲠忾钿轭珞⒘滗鲠鲠祯濠疳轵麸忾钿轭扉篝ㄣ镱ㄣ镱鲠鲠飑换项沐麇徜Ⅱ遽膦忾钿轭绗换麇汜珏蜷镦翳漉眄铒忾钿轭珞ㄩㄥ忾钿轭珞铒忾钿轭珞铋忾钿轭珞┅ㄤ彐躅鲠蜷徕戾⑸鲠蜷徕戾ㄡ簌礅镬忮玳铑轭鏖翳嗫З竣ㄡ钿簌礅镬ㄥ聃犰ㄥ祠簌礅镬钺礤癌＼咯┅换浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇换换澡湾盹辁狒轱驷汩扉豉ㄤ彐磲泸溴骢瞽礤盹ㄦ狎珞怙澌怙澌⒛彐轭礤盹辁邃骢钽糸镱啜礤盹辁ㄤ彐躅骖狎珞怙澌┅ㄤ彐躅礤盹ㄦ脲脲＇骈蝮舂翦篝＇羼飑钺礤⒁弭躜礤盹骢钽糸镱镦骖戾è翎忪磲脲栳箬翎忪呼弩翦篝┅箦翩ㄧ弭钺礤ы屙铹翎忪濠＇灬礅溽é蝈篝狎珞戾èㄦ躅汜祆脲狎珞┅眭祠轲戾鲠祯瀛忾钿鲠骘躅洵皓ㄧ弭栳箬翎忪濠ㄩ骘躅洵鲠箦翩ㄧ弭栳箬翎忪濠ㄡ痧禊骖狎珞┅┅┅┅ㄤ彐躅礤盹辁ㄦ瞽钺礤脲脲＇骈蝮舂翦篝＇羼飑⒁屦灬沐骖钺礤珈镡犰溴骈铋糸镱鏖翳礤盹辁邃鲥蝮轱町ㄣ戾狎礤盹辁骖钺礤箦翩簌礅镬骢钽糸镱骖钺礤礤盹簌礅镬骢钽糸镱骖钺礤侯犴骖钺礤弘妁脲呼弩翦篝┅ㄤ彐躅沆遽颦礤盹辁ㄦ瞽钺礤⒚戾狎翳栳箬翎忪骝镯礤盹骢钽糸镱戾è翎忪ㄧ弭骖钺礤ы屙铹┅麒孱翎忪ㄣ祢栳箬翎忪濠┅换换腻灬邃泔眇豸狒轱詈ㄤ彐篝蝓泗溴灬鲠祯ㄣ镯瘐翦淇铋飑ㄤ彐磲泸溴灬é蝈篝怙澌⒘泔眇豸狒轱翳狒汜忮屮邈豸邃灬翦怡葡颐女啜磲脲溴灬忽犰蹂＇灬礅溽ī怙澌┅ㄤ彐躅骘蜚ㄤ屐狴⒛溴灬邃泔眇豸狒轱瞵矧驽翥轸痱弼轱躞禊泔眇豸邃鲠祯瀹ㄩㄤ屐狴泔眇豸邃溴灬ㄤ屐狴鲠祯溴灬痱镧箦翩ㄤ屐狴鲠祯溴灬ㄦ躅汜祆ㄤ屐狴鲠祯溴灬┅箦翩ㄤ屐狴泔眇豸邃溴灬舂┅换换腻骝弩秕蜚搴ㄤ彐磲泸溴骝弩秕蜚钺礤脲泔铙趄蹉麸ㄩ铋糸犰泔痖弩癌箝磲轭轸獒飙泔痖弩卑┅戾è蝈箫躜沐簌礅镬И簌礅镬钺礤Л蝈箫躜沐┅ㄤ遽祆镢狒簌礅镬т遽祆镢狒瀛钺礤┅ㄡ祆镢狒簌礅镬п祆镢狒瀛钺礤┅啜痱镧ㄤ彐疳蜥礤翦蝈箫躜沐磲脲狎蜥箝烘殪飙痫轭翦癌ㄤ彐躅犰祜汜翦ī⑶弭犷屐屙孱骝镯翳蝈箫躜沐痫镬矧磲脲镱瀹ㄩㄦ殪飙痫轭翦蝈箫躜沐癌泔铙趄蹉麸鲥泗矧痫蝈箫躜沐┅ㄤ彐躅溴犰祜汜翦ì钺礤⑿灬沐铒祜铉弪铄邃邃屐屙孱忉汶轭翳痫镬鲥泗矧瘐箬屮翦钿钺礤蝈箫躜沐┅ㄩ轭轸獒飙泔痖弩癌啜磲疸＇溴犰祜汜翦祜镳蝈疱狒轭轸獒飙泔痖弩泔祆邈ì犰祜汜翦┅┅К钺礤┅ㄤ彐磲泸鏖翳蝈箫躜沐è鲠蝈箫躜沐镳糸镱犰痱雉邈舂蝈篝怙澌⑴邈豸怙澌鏖翳至怙躅麸犷轭篝犷沐镦遗酉找门戾è犰祜汜翦簌礅镬п祆镢狒瀛蝈箫躜沐┅ㄤ遽祆镢狒簌礅镬т遽祆镢狒瀛蝈箫躜沐┅ㄩ痱雉邈啜戾è鲠铋飑躅鏖钿痱雉邈痱镧箦翩鲠ì犰祜汜翦┅棱镤躅戾篌铛祆鲠颟ì溴犰祜汜翦鲠颟┅啜戾è鲠ì犰祜汜翦┅棱镤ì溴犰祜汜翦鲠颟┅┅换换氧艴弩换聃艴轶灬篝泔铘孱趔疳轵ㄤ彐躅聃艴瀛泔铘孱趔瘵ㄣ潋瘵ㄤ彐躅磲脲聃艴ī⒙蹰熹铄聃艴瀣鏖翳铒屐屙孱趔戾èㄣ镱铋铋飑┅箦翩ㄣ狎瘵瘵┅ㄤ彐躅孱聃艴ㄩ翦瘵⑸铙弪轸屙狒翳孱镦翳聃艴瀹箦翩ㄣ狎瘵箦翩蝈篝ㄣ狎瘵ㄣ镱轸屙铋飑┅瘵ㄤ彐躅溴聃艴瘵⒁屙秭犷轸屙骝镯翳骝镱镦翳聃艴瀹痫ㄣ潋瘵ㄩ铛祆ㄣ潋瘵箦翩ㄣ狎瘵瘵瘵ㄤ彐躅骝镱瘵ㄦ轵篝聃艴瀛泔铘孱趔瘵┅ㄤ彐躅屙痿聃艴瀛瘵铛祆聃艴瀛泔铘孱趔瘵┅ㄤ彐躅聃艴瀛钽镱扉篝⒘滗翳屐屙孱趔镦躺釉麸翳孱镦翳聃艴瀹箦翩ㄣ狎瘵灬篝箦翩蝈篝ㄣ狎瘵扉篝┅┅换换萧桢蚝ㄤ彐躅箫螋箦痱邃脲脲⒂矧鏖翳秕犰翦蜷铉翳箦聃孱沐箫螋ㄣ镳箦箦瘵痱邃弘妁脲┅ㄤ彐躅蝈躞瀛泔铙⒁弭躜ㄣ镱┈矧蝈躞殒轸轶羼踽麸ㄣ镱ㄩㄡ钿ㄥ耢ㄣ狎┅ㄥ耢ㄣ潋┅ㄣ镱┅换浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇ㄤ彐躅戾铉翳奖⑸扉篝镦戾铉翳笨ㄡ钿ㄣ镱箴铛祆ㄣ潋┅┅ㄤ彐躅蝈篝扉篝⒃桢蝈篝镦扉篝徭翦翳骈蝮匀遗屐屙孱趔ㄣ滗潋扉篝┅换浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇ㄤ彐躅躅轳蹂骈钿殒犷麒弪痱邃殂狒趄邋镳糸镱犰骘躅洵箫驷颟⒁弭躜扉篝镦戾狯弩镦趄邋筢糸箧轭痱邃殂狒瀣鏖翳漉痨殂狒弩蝈盹鲥洚ㄩㄡ麸趄邋ㄩㄦ躅汜祆痱邃殂狒趄邋ㄡ潢镩趄邋骘躅洵箫驷颟骘躅洵箫驷颟躅轳蹂骈钿殒犷麒弪痱邃殂狒ㄦ轵篝趄邋躅轳蹂骈钿殒犷麒弪痱邃殂狒蝈篝趄邋骘躅洵箫驷颟┅ㄤ彐躅骈钿殒犷麒弪痱邃殂狒趄邋⒛镥痱邃殂狒狃痨麸犷狒镯轭翳趄邋竣ㄩㄡ麸趄邋ㄦ躅汜祆痱邃殂狒趄邋矧ㄦ轭洵殒犷麒弪痱邃殂狒ㄦ轵篝趄邋┅ㄦ轭洵殒犷麒弪痱邃殂狒蝈篝趄邋┅┅换浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇ㄤ彐磲泸溴骈铄孱蹴弪狒邃豉疱豉疱蝈篝屐屙孱趔⒁屦蝈箦铘犷孱蹴弪狒邃豉疱鏖翳轭翦珏蝮碍町啜痱镧ㄤ彐豉疱豉疱īЖ轭翦珏ō戾铉翳屐屙孱趔暴┅ㄤ彐躅簌礅镬豉疱Л倔礅镬ì豉疱ㄥ祠К屐屙孱趔豉疱┅ㄤ彐躅簌礅镬簌礅镬豉疱簌礅镬痫箝糸镱簌礅镬К屐屙孱趔┅括祜镳骘屐屙孱轭屐屙孱趔骘骝镯泔祆邈啜溴驺镱篝犷屐屙孱椹┅换浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇ㄤ彐躅铒舡铛祆铒铛祆┅ㄤ彐躅骈蝮舡矧铋⒃桢骈蝮屐屙孱镦殒轸轶扉篝屐箦铋飚ㄩㄣ镱箴ㄦ轵篝铋飑ㄤ彐躅骈蝮舡矧箦戽⒃桢骈蝮屐屙孱镦殒轸轶扉篝屐箦轸箦戽ㄩㄣ镱箴ㄦ轵篝┅换浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇换换换锰籼犷廖由锰蔑眇狒殁殪轸换躅戾篌ㄦ怙躅漯т彐礤翳镤换ㄤ彐磲泸溴骓弭栾钺礤狎珞蝈篝怙澌换啜溴骢К钺礤К狎珞棱镤┅换换躅戾篌ㄦ怙躅漯ы狃轭麸换ㄤ彐躅磲瓠轭麸蝈篚祠箦聃孱沐骢钽糸镱蝈篝箦聃孱沐螬换⒛弩趄蹉糸鲥禊箦屐屙孱趔镦遗诱淘优颜盼门麸翳蝈篚祠换镦狃痨轭普蚊陨衔麸蝈箴邈糸鲥屐屙孱趔镦优颜盼门赢换戾è狎珈轶磲脲扉篝戾铉翳箦聃孱沐螬┅换ㄩ扉篝蝈篚祠箦聃孱沐换盹篝痫箝糸鲥骈铛换ㄡ蝌狴溟礤铙轱蝈篚祠箦聃孱沐癌┅换换狎珈轶轶磲溴轭麸扉篝镦狎珞骘遽汨汜祆换换轶翳戾铉翳镦翳祜铉弩鲥泗矧换麒孱箦聃孱沐换箦翩黹祜镳骘箦轭箦聃孱沐换黹铋黹戾铉翳箦瘵┅┅换换腻骈铄箫礤箬狎邃骢钽糸镱蠛换ㄦ戾换è滹镱瀛汜祆ㄩ换祜镳骘箦镱箦聃孱沐换骘狎镱狎珈轶换滹ㄩ扉篝ㄦ轵篝箦瘵换箦翩ㄦ轵篝狎绌换痫ㄦ轵篝箦瘵┅换箦翩ㄦ轵篝狎绌换ㄡ蝈ㄦ轵篝箦瘵椹┅换ㄡ痧禊骢钽糸镱狎珈轶舂换ㄤ锃蝈篚祠ㄩ换ㄩㄡ钿鲥泗矧蝈篚祠箦聃孱沐换ㄡ蝌狴栳蟓骈祆痫轭翦颦蝈篚祠箦聃孱沐┅换箦翩ㄦ殪飙痫轭翦蝈篚祠箦聃孱沐换磲ㄦ殪飙痫轭翦蝈篚祠箦聃孱沐┅┅┅换ㄤ邈灬蝈ㄩ铎轭滹镱瀛汜祆┅换换腻汩溴殒翳蝈篚祠轶扉篝矧鲥泗矧换换犷祜镳翳蝻蹒遽汨屐屙孱换ㄩ扉篝蝈篚祠箦聃孱沐换祜镳骘骝镯麸ō暴换骘镱蝈篚祠箦聃孱沐换滹箦翩ㄦ轵篝颟换ㄤ锃镱瀛汜祆椹换骈钺祆ㄤ锃蝈篚祠椹换祜镳骘骝镯麸ō暴换滹箦翩ㄡ蝈蝈篚祠箦聃孱沐椹换ㄤ锃镱瀛汜祆椹换骈钺祆ㄤ锃蝈篚祠椹┅换蝈篚祠箦聃孱沐┅换换躅戾篌ㄦ怙躅漯с镯痨屙孱舂换ㄤ彐躅泔眇戾礤铘ㄦ瞟换⑸莆蝈趱蝾翳孱ㄣ镯痨屙孱莆蝈趱蝾铒┊换＇灬礅溽é蝈篝狎珞铒ㄡ痧禊骖狎珞┅┅换换躅戾篌ㄦ怙躅漯鏖翳泔眇殪狒轱瞽躅轸换ㄤ彐磲泸鏖翳泔眇殪狒轱瞽躅轸镳糸镱怙澌怙澌换⒛翳怙澌怩溴灬泔眇殪弪麽蝾轭珞躅糸翳孱洚换换澡狒麽躅溴骈铄骢钽糸镱麽蝾轭珞翳狒狎蝈犰禊换换牾篝骘蝼狎蝈驽蝈钽弩鏖祆铒忮痱轭翦狒犰飚换换澡轶轶溴骈铄轭蔑眄镱涕箴翳提铉踽珏差邃换ㄤ邈灬蝈ㄩ珙矧镳糸镱螬换啜蝈徜糸礤汜箦换＋涕箴с镯痖戾蚝泔眇殪弪麽蝾轭珞泔铘屮舡忾钿换＋条汩鏖翳溴驽蝌邃麽蝾轭珞换ю蝻珙换怙澌┅换换换换义漉沐换麒孱铋换描犷珏翳轶麸殒秕铄邃遗恼门鏖翳弘妁脲黠蜾换ㄤ彐躅蝈漉沐ㄦ箦骝镯孱篝狎孱脲轭轸轭轸皓换ㄦ躅汜祆ㄩ扉篝箦瘵＇蝈漉沐扉篝＇蝈漉沐鲥泗换骖箦骝镯孱矧篝狎癌孱脲轭轸轭轸皓换ㄤ彐躅蝈漉沐ㄦ躅泗轱箦聃孱沐脲骝镯孱篝狎孱脲换ㄩ铋糸犰鲠祯铋轭轸獒飙鲠祯瀛皓换蝈漉沐骢钽糸镱箦聃孱沐骝镯孱篝狎孱换脲轭轸獒飙鲠祯轭轸獒飙鲠祯瀛皓换ㄤ彐躅蝈漉沐鲥泗ㄦ箦骝镯孱篝狎孱脲轭轸轭轸皓换ㄩ铛祆孱洎箦翩孱戾铉翳箦瘵┅换ㄡ篌弪冀篝狎孱戾铉翳箦瘵篝狎孱洎换⑸祆彗犰篚怏羼蹂钽镦后翎螋哄钿洧换箦篝狎孱洎换ㄣ狍ō孱篝狎舂换ūㄩ轭轸换ㄦ躅汜祆骖轭轸ㄦ躅汜祆殒脲ㄡ蝈箦篝狎舂┅换ㄦ躅汜祆殒脲ㄡ蝈箦篝狎舂┅换òㄩ轭轸轭轸ㄦ躅汜祆骖┅换ㄩ铒骝镯孱洎换戾è蝈篚祠换ㄩ轭轸换ㄦ躅汜祆换骖轭轸换ㄦ躅汜祆殒脲ㄡ蝈箦篝狎舂┅换ㄦ躅汜祆换骖换ㄦ躅汜祆殒脲ㄡ蝈箦篝狎舂换ㄦ躅汜祆殒脲ㄡ蝈箦ǐ篝狎暴┅┅┅换祜镳骘骝镯ǐ篝狎ㄩ轭轸博换麸ō孱暴换滹箦翩蝈篚祠换ㄦ躅汜祆换骖蝈篚祠换ㄦ躅汜祆殒脲ㄡ蝈箦椹┅┅换蝈篚祠换戾è蝈篚祠换ㄩ轭轸换ㄦ躅汜祆换骖换ㄦ躅汜祆殒脲ㄡ蝈箦ō孱暴┅换轭轸换ㄦ躅汜祆换骖换ㄦ躅汜祆殒脲ㄡ蝈箦ō孱博┅换ㄦ躅汜祆殒脲ㄡ蝈箦ō孱暴┅┅┅换祜镳骘骝镯ō孱ㄩ轭轸畅滹黝麸篝狎换滹箦翩蝈篚祠换ㄦ躅汜祆换骖换ㄦ躅汜祆殒脲ㄡ蝈箦椹换蝈篚祠┅换蝈篚祠┅┅换ㄤ彐躅蝈漉沐扉篝ㄦ箦骝镯孱篝狎孱脲轭轸轭轸皓换ㄩ铛祆孱洎箦翩孱戾铉翳箦瘵┅换ㄣ镱è篝狎癌换蝈漉沐扉篝骖铘桡潋篝狎箦瘵骝镯孱换ō孱篝狎舂脲轭轸轭轸皓换è矧铛祆箦瘵ㄥ耢篝狎孱洎换ㄩ轭轸轭轸ㄦ躅汜祆骖┅换èō孱篝狎舂暴换ㄩ轭轸换ㄦ躅汜祆骖轭轸ㄦ躅汜祆殒脲ㄦ轵篝箦瘵┅换ㄦ躅汜祆殒脲ㄦ轵篝箦瘵┅换ㄦ蝻憝孱换蝈漉沐鲥泗骖ㄣ镥蜚箦鲥泗矧篝狎孱换脲轭轸轭轸皓换è铛祆蝈篝箦瘵换ㄩ轭轸换ㄦ躅汜祆骖轭轸ㄦ躅汜祆殒脲ㄦ轵篝箦瘵┅换ㄦ躅汜祆殒脲ㄦ轵篝箦瘵┅换戾è蝈篚祠换ㄩ轭轸换ㄦ躅汜祆换骖轭轸换ㄦ躅汜祆殒脲痫箦瘵┅换ㄦ躅汜祆换骖换ㄦ躅汜祆殒脲痫箦瘵换ㄦ躅汜祆殒脲痫箦瘵┅┅换ㄩ孱换祜镳蝈疱狒ō孱ㄩ轭轸博麒殪箦换滹箦翩蝈篚祠换ㄦ躅汜祆换骖蝈篚祠换ㄦ躅汜祆殒脲痫箦瘵┅┅换祜镳麒殪箦换滹箦翩蝈篚祠换ㄦ躅汜祆换骖蝈篚祠换ㄦ躅汜祆殒脲痫箦瘵┅┅换蝈篚祠┅┅换